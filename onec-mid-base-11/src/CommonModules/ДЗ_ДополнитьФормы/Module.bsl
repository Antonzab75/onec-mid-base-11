#Область ПрограммныйИнтерфейс

Процедура ДополнитьФормыДокументов(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
		
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма); 
		ВставитьПолеСогласованнаяСкидкаИКомандуПересчитатьСкидкуНаФорму(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда 
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);	
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);  
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);  
	КонецЕсли
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)   
	Если Форма.Элементы.Найти("КонтактноеЛицо") = Неопределено Тогда
		ПолеВВода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"),Форма.Элементы.ГруппаШапкаПраво);
		ПолеВВода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВВода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо"; 
	КонецЕсли;
КонецПроцедуры 

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма) 
	Если Форма.Элементы.Найти("КонтактноеЛицо") = Неопределено Тогда
		ПолеВВода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),,Форма.Элементы.Ответственный);
		ПолеВВода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВВода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо"; 
	КонецЕсли;
КонецПроцедуры  

Процедура ВставитьПолеСогласованнаяСкидкаИКомандуПересчитатьСкидкуНаФорму(Форма) 
	
	Если Форма.Элементы.Найти("ГруппаСкидка") = Неопределено Тогда
		ГруппаФормы = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
		ГруппаФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаФормы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда; 
		ГруппаФормы.ОтображатьЗаголовок = Ложь;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("СогласованнаяСкидка") = Неопределено Тогда
		ПолеВВода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"),Форма.Элементы.ГруппаСкидка);
		ПолеВВода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВВода.ПутьКДанным = "Объект.ДЗ_СогласованнаяСкидка"; 
		Форма.Элементы.СогласованнаяСкидка.УстановитьДействие("ПриИзменении", "ДЗ_СогласованнаяСкидкаПриИзмененииПосле");

	КонецЕсли;
	
	Если Форма.Элементы.Найти("ПересчитатьТаблицу") = Неопределено Тогда
		КомандаФормы = Форма.Команды.Добавить("ПересчитатьТаблицу");
		КомандаФормы.Заголовок = "Пересчитать таблицу";
		КомандаФормы.Действие = "ДЗ_ПересчитатьТаблицуПосле";
		
		КнопкаКоманды = Форма.Элементы.Вставить("КомандаФормы", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
		КнопкаКоманды.Вид = ВидКнопкиФормы.ОбычнаяКнопка;  
		КнопкаКоманды.ИмяКоманды = "ПересчитатьТаблицу";
		КнопкаКоманды.Картинка = БиблиотекаКартинок.Обновить;
		КнопкаКоманды.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти